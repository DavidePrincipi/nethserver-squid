#
# 90squid
#
{
    use esmith::NetworksDB;
    use esmith::ConfigDB;
    use NethServer::Firewall;
    my $fw = new NethServer::Firewall();
    my $ndb = esmith::NetworksDB->open_ro();
    my $pdb = esmith::ConfigDB->open_ro('fwrules');
    my $status = $squid{'status'} || 'disabled';
    my $green_mode = $squid{'GreenMode'} || 'manual';
    my $blue_mode = $squid{'BlueMode'} || 'manual';
    my $block = $squid{'PortBlock'} || 'disabled';
    
    my %zones;
    foreach my $i ($ndb->interfaces) {
        my $role = $i->prop('role') || '';
        next if ($role eq '' || $role eq 'red' || $role eq 'slave' || $role eq 'bridged' || $role eq 'alias');
        my $zone = substr($role, 0, 5); #truncate zone name to 5 chars
        if ($role eq 'green') { # rename green to loc
            $zone = 'loc';
        }
        $zones{$zone} = ''; # avoid duplicate policies
    }


    if ($status eq 'enabled') {
        my @bypass_src = ();
        my $bypass_src_str = '';
        my @bypass_dst = ();
        my $bypass_dst_str = '';

        if ( $green_mode =~ /transparent/ || (defined($ndb->blue()) && $blue_mode =~ /transparent/) ) {

            foreach ($pdb->get_all_by_prop(type => 'bypass-src')) {
                my $status = $_->prop('status') || 'disabled';
                next if ($status eq 'disabled');
                my $host = $_->prop('Host') || next;
                push(@bypass_src,$fw->getAddress($host))
            }
            foreach ($pdb->get_all_by_prop(type => 'bypass-dst')) {
                my $status = $_->prop('status') || 'disabled';
                next if ($status eq 'disabled');
                my $host = $_->prop('Host') || next;
                push(@bypass_dst,$fw->getAddress($host))
            }

        }
        if (scalar(@bypass_src)>0) {
            $bypass_src_str = ":!".join(',',@bypass_src);
        } 
        if (scalar(@bypass_dst)>0) {
            $bypass_dst_str = "!".join(',',@bypass_dst);
        } 

        if ($green_mode =~ /transparent/) {
            $OUT.="#\n# Squid: accept HTTP/S traffic from/to firewall and green\n#\n";
            $OUT.="ACCEPT\t\$FW\tnet\ttcp\t80\n";
            $OUT.="ACCEPT\tloc\t\$FW\ttcp\t80\n";
            $OUT.="ACCEPT\tloc\t\$FW\ttcp\t443\n";

            $OUT .="?COMMENT transparent proxy on green for port 80\n";
            $OUT.="REDIRECT\tloc$bypass_src_str\t3129\ttcp\t80\t-\t$bypass_dst_str\n";
            if ($green_mode =~ /ssl/) {
                $OUT .="?COMMENT transparent proxy on green for port 443\n";
                $OUT.="REDIRECT\tloc$bypass_src_str\t3130\ttcp\t443\t-\t$bypass_dst_str\n";
            }

        }
        if (defined($ndb->blue()) && $blue_mode =~ /transparent/) {
            $OUT.="#\n# Squid: accept HTTP/S traffic from/to firewall and blue\n#\n";
            $OUT.="ACCEPT\t\$FW\tnet\ttcp\t80\n";
            $OUT.="ACCEPT\tblue\t\$FW\ttcp\t80\n";
            $OUT.="ACCEPT\tblue\t\$FW\ttcp\t443\n";

            my $ports = $squid{'TCPPorts'} || '';
            foreach( split(/,/,$ports) ) {
                $OUT.="ACCEPT\tblue\t\$FW\ttcp\t$_\n";
            }


            $OUT .="?COMMENT transparent proxy on blue for port 80\n";
            $OUT.="REDIRECT\tblue$bypass_src_str\t3129\ttcp\t80\t-\t$bypass_dst_str\n";
            if ($blue_mode =~ /ssl/) {
                $OUT .="?COMMENT transparent proxy on blue for port 443\n";
                $OUT.="REDIRECT\tblue$bypass_src_str\t3130\ttcp\t443\t-\t$bypass_dst_str\n";
            }
        }
    }

    if ($status eq 'enabled' && $block eq 'enabled') {
        # generate rules for all zones
        foreach my $z (keys %zones) {
            $OUT .= "#\n# Block HTTP/HTTPS from $z to net\n#\n";
            $OUT .="?COMMENT Proxy block HTTP/HTTPS ports\n";
            $OUT .= "REJECT $z\t\tnet\ttcp\t80,443\n";
        }
   }
    
}
